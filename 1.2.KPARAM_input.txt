[[init]]
iterators = sample ration numbering;
sample = 100 200 300;
ration = 1:31;
numbering = 1:500;
ratio#ration = -1.5 -1.4 -1.3 -1.2 -1.1 -1.0 -0.9 -0.8 -0.7 -0.6 -0.5 -0.4 -0.3 -0.2 -0.1 0 0.1 0.2 0.3 0.4 0.5 0.6 0.7 0.8 0.9 1.0 1.1 1.2 1.3 1.4 1.5;
outputDirectory = "C:/Users/junye/desktop/500replication/01.apimk/01.DATA_GENERATION/[[sample]]/Condition[[ration]]_[[ratio#ration]]";
filename = "Step2.External_Monte_Carlo_Phantom_k_parameter_K_[[ratio#ration]]_S_[[sample]]_[[numbering]].inp";
[[/init]]

TITLE: Step2.k_[[ratio#ration]]_s_[[sample]]_[[numbering]]

DATA: FILE IS apim_k_[[ratio#ration]]_s_[[sample]]_[[numbering]].dat;

VARIABLE: NAMES ARE Y1 Y2 X1 X2;

ANALYSIS: ESTIMATOR = ML;
          BOOTSTRAP = 2000;

MODEL:
P1 BY Y1*(K1); ! '*' is used for freeing the factor loading
P2 BY Y2*(K2); ! '*' is used for freeing the factor loading

Y1 ON X1 (A1); !actor effect for member 1
Y2 ON X2 (A2); !actor effect for member 2

P1 ON X2 (A1); !path from X2 to P1 is constrained as A1
P2 ON X1 (A2); !path from X1 to P2 is constrained as A2

P1@0; !disturbance of P1 is constrained as 0
P2@0; !disturbance of P2 is constrained as 0
P1 WITH P2@0; !covariance between P1 and P2 is constrained as 0

X1 WITH X2; !covariance between predictors
Y1 WITH Y2; !covariance between outcomes

OUTPUT: STDYX SAMPSTAT CINTERVAL(BCBOOTSTRAP);